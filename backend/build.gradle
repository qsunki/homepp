plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id "com.diffplug.spotless" version "6.25.0"
}

group = 'ssafy.age'
version = '0.0.1-SNAPSHOT'
description = 'homepp-server'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	create 'mockitoAgent'
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.google.firebase:firebase-admin:9.3.0'
	implementation 'org.springframework.integration:spring-integration-mqtt:6.3.2'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	developmentOnly "org.springframework.boot:spring-boot-docker-compose"

	// https://mvnrepository.com/artifact/com.google.guava/guava
	implementation 'com.google.guava:guava:33.2.1-jre'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// Swagger
	implementation 'io.springfox:springfox-swagger2:3.0.0'
	implementation 'io.springfox:springfox-swagger-ui:3.0.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
	//thumbnail
	implementation 'org.jcodec:jcodec:0.2.5'
	implementation 'org.jcodec:jcodec-javase:0.2.5'

	mockitoAgent("org.mockito:mockito-core") {
		transitive = false
	}
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
	jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}"
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

spotless {

	format 'misc', {
		target '*.gradle', '.gitattributes', '.gitignore'

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}

	format 'yaml', {
		target '**/*.yml'

		trimTrailingWhitespace()
		indentWithSpaces(2)
		endWithNewline()
	}

	java {
		removeUnusedImports()
		googleJavaFormat().aosp().reflowLongStrings().skipJavadocFormatting()
		formatAnnotations()
	}
}
